plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.sonarqube' version '5.0.0.4638'
    id 'jacoco'
}

allprojects {
    group = 'org.yyubin'
    version = 'v1'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
    }

    sonarqube {
        properties {
            property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }
}

// Aggregate JaCoCo coverage report for all subprojects
task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    group = 'verification'

    dependsOn subprojects*.test

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.required = true
        xml.required = true
        csv.required = false
        html.outputLocation = file("${buildDir}/reports/jacoco/test/html")
        xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }

    doFirst {
        executionData.from = files(executionData.findAll { it.exists() })
    }
}

sonarqube {
    properties {
        def sonarProjectKey = System.getProperty('sonar.projectKey') ?: System.getenv('SONAR_PROJECT_KEY')
        def sonarHostUrl = System.getProperty('sonar.host.url') ?: System.getenv('SONAR_HOST_URL')
        def sonarToken = System.getProperty('sonar.token') ?: System.getenv('SONAR_TOKEN')

        if (sonarProjectKey) {
            property 'sonar.projectKey', sonarProjectKey
        }
        if (sonarHostUrl) {
            property 'sonar.host.url', sonarHostUrl
        }
        if (sonarToken) {
            property 'sonar.token', sonarToken
        }

        property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

tasks.named('sonarqube') {
    dependsOn jacocoRootReport
}
